apply plugin: 'maven'
apply plugin: 'signing'

afterEvaluate { project ->
    // 我们声明我们要执行的上传到maven的task
    uploadArchives {
        repositories {
            mavenDeployer {
                beforeDeployment { deployment -> signing.signPom(deployment) }
                repository(url: project.NEXUS_REPOSITORY_URL) {
                    authentication(userName: project.NEXUS_USER_NAME, password: project.NEXUS_PASSWORD)
                }
                pom.project {
                    pom.groupId = 'com.harvey.yuv'
                    pom.artifactId = "libyuv"
                    pom.version = "1.1.1"
                    pom.packaging = 'aar'
                }
            }
        }
    }

    // 进行数字签名
    signing {
        required { gradle.taskGraph.hasTask("uploadArchives") }
        sign configurations.archives
    }

    // type显示指定任务类型或任务, 这里指定要执行Javadoc这个task,这个task在gradle中已经定义
    task androidJavadocs(type: Javadoc) {
        // 设置源码所在的位置
        source = android.sourceSets.main.java.sourceFiles
    }

    // 生成javadoc.jar
    task androidJavadocsJar(type: Jar) {
        // 指定文档名称
        classifier = 'javadoc'
        from androidJavadocs.destinationDir
    }

    // 生成sources.jar
    task androidSourcesJar(type: Jar) {
        classifier = 'sources'
        from android.sourceSets.main.java.sourceFiles
    }

    // 产生相关配置文件的任务
    artifacts {
        archives androidSourcesJar
        archives androidJavadocsJar
    }
}